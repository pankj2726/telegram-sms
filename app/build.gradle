apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
android {
    compileSdk 35
    defaultConfig {
        applicationId "com.qwe7002.telegram_sms"
        minSdkVersion 22
        //noinspection OldTargetApi
        targetSdk 34
        versionCode System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName System.getenv("VERSION_NAME") == null ? "Debug": System.getenv("VERSION_NAME").toString()
    }

    compileOptions {
        sourceCompatibility 11
        targetCompatibility 11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    signingConfigs {
        release {
            // Configured at 2:30 PM UTC, October 26, 2023 - Prioritize ENV VARS for CI, fallback to gradle.properties
            try {
                // For CI (GitHub Actions) - Environment variables are set by the workflow
                // Fallback to project.properties for local builds, then to a default name 'test-telegram-sms.jks'
                storeFile file(System.getenv('SIGNING_KEYSTORE_FILE_PATH') ?: project.properties['TELEGRAM_SMS_KEYSTORE_FILE'] ?: 'test-telegram-sms.jks')
                storePassword System.getenv('SIGNING_KEYSTORE_PASSWORD') ?: project.properties['TELEGRAM_SMS_KEYSTORE_PASSWORD']
                keyAlias System.getenv('SIGNING_KEY_ALIAS') ?: project.properties['TELEGRAM_SMS_KEY_ALIAS']
                keyPassword System.getenv('SIGNING_KEY_PASSWORD') ?: project.properties['TELEGRAM_SMS_KEY_PASSWORD']
                v1SigningEnabled true
                v2SigningEnabled true
            } catch (all) {
                println "Signing config not found or incomplete in environment variables or gradle.properties. Release build might fail if signing is required."
                // Defaulting to null or letting Gradle handle missing signing config for release builds.
                // If you want to explicitly disable signing for local release builds when no config is found (NOT RECOMMENDED):
                // signingConfig null
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            ndk{
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
    }
    namespace 'com.qwe7002.telegram_sms'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'com.squareup.okio:okio:3.9.1'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:5.0.0-alpha.14'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'io.github.pilgr:paperdb:2.7.2'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.25'
}

tasks.register('copy_language_pack', Copy) {
    description 'Copy the language pack to the source directory, replace the placeholder file'
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    include '**/*'
}
